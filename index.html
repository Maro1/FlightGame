<!DOCTYPE html>
<html>

<head>
    <title>Lighting</title>
    <style type="text/css">
        canvas {
            border: 1px solid gray;
        }
    </style>
</head>

<body>
    <canvas id="main_canvas"></canvas>

    <script id="vertex-shader" type="x-shader/x-vertex">
            precision mediump float;

            attribute vec3 a_Pos;
            attribute vec3 a_Normal;
            attribute vec2 a_TexCoord;
            attribute vec3 a_Tangent;
            attribute vec3 a_Bitangent;

            varying vec3 v_Normal;
            varying vec3 v_FragPos;
            varying vec2 v_TexCoord;

            varying vec4 v_FragPosLight;

            varying mat3 TBN;

            varying vec3 v_Pos;

            uniform mat4 model;
            uniform mat4 view;
            uniform mat4 proj;

            uniform mat4 transInvModel;
            uniform mat4 lightSpaceMatrix;

            void main() 
            {
                v_Normal = mat3(transInvModel) * a_Normal; 
                v_FragPos = vec3(model * vec4(a_Pos, 1.0));
                v_TexCoord = a_TexCoord;
                v_FragPosLight = lightSpaceMatrix * vec4(v_FragPos, 1.0);

                vec3 T = normalize(vec3(model * vec4(a_Tangent,   0.0)));
                vec3 B = normalize(vec3(model * vec4(a_Bitangent, 0.0)));
                vec3 N = normalize(vec3(model * vec4(a_Normal,    0.0)));
                TBN = mat3(T, B, N);

                v_Pos = (view * model * vec4(a_Pos, 1.0)).xyz;

                gl_Position = proj * view * model * vec4(a_Pos, 1.0); 
            }
        </script>
    <script id="fragment-shader" type="x-shader/x-fragment">
            precision highp float;

            varying vec3 v_Normal;
            varying vec3 v_FragPos;
            varying vec2 v_TexCoord;

            varying vec4 v_FragPosLight;

            varying mat3 TBN;

            varying vec3 v_Pos;

            uniform sampler2D diffuseMap;
            uniform sampler2D specularMap;
            uniform sampler2D normalMap;
            uniform sampler2D shadowMap;

            uniform samplerCube skybox;

            uniform vec3 viewPos;
            uniform vec3 lightDir; 

            uniform int useShadows;
            uniform int useNormalMap;

            uniform float fogAmount;

            float shadow()
            {
                if (useShadows == 0) {
                    return 1.0;
                }
                float bias = 0.00001;  
                vec3 pos = v_FragPosLight.xyz * 0.5 + 0.5 - bias;
                float depth = texture2D(shadowMap, pos.xy).r;
                if (depth < pos.z) {
                    return 0.0;
                }
                else {
                    return 1.0;
                }

            }

            void main() {
                vec3 normal;
                if (useNormalMap == 1) {
                    normal = texture2D(normalMap, v_TexCoord).rgb;
                    normal = 2.0 * normal - 1.0;
                    normal = normalize(TBN * normal);
                }
                else {
                    normal = normalize(v_Normal);
                }

                vec3 kD = texture2D(diffuseMap, v_TexCoord).rgb;
                vec3 kS = texture2D(specularMap, v_TexCoord).rgb;

                vec3 ambient = kD * 0.3;

                vec3 diffuse = kD * max(dot(lightDir, normal), 0.0);

                vec3 viewDir = normalize(v_FragPos - viewPos);
                vec3 reflectDir = reflect(viewDir, normal);

                vec3 specular = kS * textureCube(skybox, reflectDir).rgb * pow(max(dot(viewDir, reflectDir), 0.0), 16.0);;

                vec4 color = vec4(ambient + shadow() * (diffuse + specular), 1.0);

                if (fogAmount != 0.0) {
                    float fog = smoothstep((1.0 - fogAmount) * 10000.0, (1.0 - fogAmount) * 20000.0, length(v_Pos));
                    gl_FragColor = mix(color, vec4(0.8, 0.9, 1.0, 1.0), fog);
                }
                else {
                    gl_FragColor = color;
                }
            }
        </script>
    <script id="shadow-vs" type="x-shader/x-vertex">
            precision mediump float;

            attribute vec3 a_Pos;

            uniform mat4 model;
            uniform mat4 lightSpaceMatrix;

            void main() {
                gl_Position = lightSpaceMatrix * model * vec4(a_Pos, 1.0); 
            }
        </script>
    <script id="shadow-fs" type="x-shader/x-fragment">
            void main() 
            {
                gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
            }
        </script>

    <script id="light-shader" type="x-shader/x-fragment">
            void main() 
            { 
                gl_FragColor = vec4(1.0); 
            }
        </script>
    <script id="skybox-vs" type="x-shader/x-vertex">
            attribute vec3 vPosition;
            varying vec4 fPosition;

            void main() {
                fPosition = vec4(vPosition, 1.0);
                gl_Position = vec4(vPosition, 1.0);
            }
        </script>
    <script id="skybox-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec4 fPosition;

            uniform samplerCube skybox;
            uniform mat4 viewProjInv;
            uniform float fogAmount;


            void main() 
            {
                vec4 t = viewProjInv * fPosition;

                vec4 color = textureCube(skybox, normalize(t.xyz / t.w));
                if (fogAmount == 0.0) {
                    gl_FragColor = color;
                }
                else {
                    gl_FragColor = mix(color, vec4(0.8, 0.9, 1.0, 1.0), pow((1.0 - t.y), 1.0 / fogAmount));
                }
            }
        </script>
    <script type="text/javascript" src="resources/mesh_utils.js"></script>
    <script type="text/javascript" src="resources/glm_utils.js"></script>
    <script type="text/javascript" src="resources/webgl-utils.js"></script>
    <script type="text/javascript" src="resources/m4.js"></script>
    <script type="text/javascript" src="resources/jquery-3.6.0.js"></script>
    <script type="text/javascript" src="resources/dat.gui.js"></script>

    <script type="text/javascript" src="texture.js"></script>
    <script type="text/javascript" src="mesh.js"></script>
    <script type="text/javascript" src="camera.js"></script>
    <script type="text/javascript" src="airplane.js"></script>
    <script type="text/javascript" src="scene.js"></script>
    <script type="text/javascript" src="main.js"></script>
</body>

</html>